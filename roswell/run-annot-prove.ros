#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#

(unless (find-package :uiop)
  (ql:quickload :uiop :silent t))

(ql:quickload '(:prove :cl-annot-prove) :silent t)

(when (uiop:getenv "COVERALLS")
  (ql:quickload '(:cl-coveralls :split-sequence) :silent t))

(defun main (&rest option-and-test-files)
  (labels ((run-tests (with-prove test-files)
             (not
              (some #'null
                    (mapcar (lambda (test-file)
                              (let ((test-file (pathname test-file)))
                                (unless (or (probe-file test-file)
                                            (string= (pathname-type test-file) "asd"))
                                  (error "~S is not an asd file." test-file))
                                (load test-file)
                                (let ((system (asdf:find-system (pathname-name test-file))))
                                  (and (annot.prove:run-system-tests system)
                                       (if with-prove
                                           (prove:run-test-system system)
                                           t)))))
                            test-files)))))
    (multiple-value-bind (with-prove test-files)
        (loop with with-prove
              with test-files
              for item in option-and-test-files
              do (cond
                   ((or (string-equal item "-p")
                        (string-equal item "--with-prove"))
                    (setq with-prove t))
                   ((or (string-equal item "-P")
                        (string-equal item "--without-prove"))
                    (setq with-prove nil))
                   (t (push item test-files)))
              finally (return (values with-prove test-files)))
      (or #.(if (uiop:getenv "COVERALLS")
                `(,(intern (string :with-coveralls) :coveralls)
                  (:exclude
                   (,(intern (string :split-sequence) :split-sequence)
                    #\: (or (uiop:getenv "COVERAGE_EXCLUDE") "")
                    :remove-empty-subseqs t))
                  (run-tests with-prove test-files))
                '(run-tests with-prove test-files))
          (uiop:quit -1)))))
